<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE concept PUBLIC "-//OASIS//DTD DITA Concept//EN" "concept.dtd">
<concept id="gets">
 <title id="x-1">Example: Request information</title>
 <conbody id="x-2">
  <p id="x-3">Likewise, you can get request information inside a <codeph id="x-7">Widget</codeph>. Here we
            can determine the sort order of a list based on a GET parameter.</p>
  <codeblock id="x-4" outputclass="haskell">{-# LANGUAGE OverloadedStrings, TypeFamilies, TemplateHaskell,
             QuasiQuotes, TypeFamilies, MultiParamTypeClasses, GADTs #-}
import Yesod
import Data.Text (Text)
import Data.List (sortBy)
import Data.Ord (comparing)

data Person = Person
    { personName :: Text
    , personAge :: Int
    }

people :: [Person]
people =
    [ Person "Miriam" 25
    , Person "Eliezer" 3
    , Person "Michael" 26
    , Person "Gavriella" 1
    ]

data People = People

mkYesod "People" [parseRoutes|
/ RootR GET
|]

instance Yesod People

instance RenderMessage People FormMessage where
    renderMessage _ _ = defaultFormMessage


getRootR :: Handler RepHtml
getRootR = defaultLayout [whamlet|
&lt;p&gt;
    &lt;a href="?sort=name"&gt;Sort by name
    \ | #
    &lt;a href="?sort=age"&gt;Sort by age
    \ | #
    &lt;a href="?"&gt;No sort
^{showPeople}
|]

showPeople :: Widget
showPeople = do
    msort &lt;- lift $ runInputGet $ iopt textField "sort"
    let people' =
            case msort of
                Just "name" -&gt; sortBy (comparing personName) people
                Just "age"  -&gt; sortBy (comparing personAge)  people
                _           -&gt; people
    [whamlet|
&lt;dl&gt;
    $forall person &lt;- people'
        &lt;dt&gt;#{personName person}
        &lt;dd&gt;#{show $ personAge person}
|]

main :: IO ()
main = warpDebug 3000 People</codeblock>
  <p id="x-5">Once again, all we need to do is <codeph id="x-6">lift</codeph> our normal
                <codeph id="x-8">Handler</codeph> code (in this case, <codeph id="x-9">runInputGet</codeph>) to have
            it run in our <codeph id="x-10">Widget</codeph>.</p>
 </conbody>
</concept>
